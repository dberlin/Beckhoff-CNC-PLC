<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.16">
  <POU Name="MAIN" Id="{cd64af0a-d51b-49f0-91ce-918439305f15}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	{region "Servo handling variables"}
	// Alarm inputs from Servos
  	iXAxisAlarm AT %I*:BOOL;
  	iYAxisAlarm AT %I*:BOOL;
  	iZAxisAlarm AT %I*:BOOL;
  	iAAxisAlarm AT %I*:BOOL;
  	// Alarm Output to PC pin 8
  	oPCServoAlarm AT %Q*:BOOL;
  	// Whether servo thinks Z Axis break should be off
  	iZAxisBrakeOff AT %I*:BOOL;
	// Relay output controlling Z Axis Brake
	oZAxisBrakeRelay AT %Q*: BOOL;
    {endregion}
	{region "Probe, Air Pressure, Coolant, Vacuum"}
	// Digital Probe Trigger output to PC pin 29
	oPCProbe AT %Q*: BOOL;
	// Digital Probe Input (Normally Closed)
	iProbe AT %I*: BOOL := TRUE;
	// Air Pressure >90psi signal to PC pin 10
	oPCAirPressure AT %Q*: BOOL;
	// Air Pressure >90psi signal from sensor.  The sensor triggers as pressure is rising past 90psi
	iAirPressureSensor AT %I*: BOOL;
	// Control of NITRA Valves over ethernet/ip	 
	oValveControl AT %Q* : NitraValves; 
	// Turn on coolant signal from PC
	iPCCoolantOn AT %I* : BOOL;
	// Turn on vacuum signal from PC
	iPCVacuumOn AT %I* : BOOL;
	// Vacuum Control Output
	oVacuumControl AT %Q* : CIPExtendedSpeedOut;
	// Vacuum Pressure good output to PC Pin 7
	oPCVacuumPressureGood AT %Q*: BOOL;
	{endregion}
	{region "Spindle variables"}
	{region "Spindle Speed Handling"}
	// True if spindle is at less than 1 RPM
	SpindleStopped : BOOL;
	// Spindle stopped output to PC pin 1
	oPCSpindleStopped AT %Q* : BOOL;
	// Spindle within 5% of speed to PC Pin 28
	oPCSpindleSpeedOK AT %Q* : BOOL;
	{endregion}
	{region "Spindle Tool Change handling"}
	// Tool Release Button on Spindle
	iSpindleToolReleaseButton AT %I*: BOOL;
	// Tool Release from PC Pin 24
	iPCToolRelease AT %I*: BOOL;
	// Tool Presence to PC Pin 2
	oPCToolPresence AT %Q* : BOOL;
	// S1 Tool Presence sensor from the spindle
	iS1ToolPresence AT %I* : BOOL;
	// Collet Open to PC Pin 9
	oPCColletOpen AT %Q* : BOOL;
	// S2 Collet Open sensor from the spindle
	iS2ColletOpen AT %I* : BOOL;	 
	// Spindle over temperature to PC Pin 27
	oPCSpindleOverTemp AT %Q* : BOOL;
	// Spindle over temperature relay
	iSpindleOverTemp AT %I* : BOOL;
	// Turn spindle on input from PC
	iPCSpindleOn AT %I* : BOOL;
	// Delta VFD control for the spindle
	oSpindleControl AT %Q* : DeltaVFDCommandWord;
	oSpindleFrequency AT %Q* : UINT;
	{endregion}
	{endregion}
	{region "Input Debouncers"}
	(* The 5V input has a ridiculously fast time - a few nanoseconds.
	   Therefore, we debounce it *)
	CoolantOnDebounced : TON;
	SpindleOnDebounced : TON;
	VacuumOnDebounced : TON;
	ToolReleaseDebounced : TON;	
	{endregion}
END_VAR

VAR CONSTANT
	// Minimum PSI at which vacuum holding is viable for workpiece
	GoodVacuumPressure : REAL := 10.0;
	// Number of RPM per HZ in the VFD
	HZPerRPM : UINT := 30;
	// VFD frequency precision in 1/X of 1 HZ. So 10 means it can do 1/10th of a HZ
	VFDHZPrecision : UINT := 10;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[CoolantOnDebounced(IN := iPCCoolantOn, PT := T#3MS);
SpindleOnDebounced(IN := iPCSpindleOn, PT := T#3MS);
VacuumOnDebounced(IN := iPCVacuumOn, PT := T#3MS);
ToolReleaseDebounced(IN := iPCToolRelease, PT := T#3MS);

{region "Servo handling"}
// Servo Alarm
	// The servo alarm inputs are normally closed
	oPCServoAlarm := NOT (iXAxisAlarm AND iYAxisAlarm AND iZAxisAlarm AND iAAxisAlarm);

// Servo Brake	 
	oZAxisBrakeRelay := iZAxisBrakeOff;
{endregion}

{region "Probe, Air Pressure, Coolant, Vacuum"}
// Probe control
	oPCProbe := NOT iProbe;

// Pressure Control
	oPCAirPressure := iAirPressureSensor;

// Colant control
	oValveControl.Coolant := CoolantOnDebounced.Q;

// Vacuum control	
	oVacuumControl.RunFwd := VacuumOnDebounced.Q;
	oVacuumControl.RunReverse := FALSE;
	oVacuumControl.NetCtrl := TRUE;
	oVacuumControl.NetRef := TRUE;
	oPCVacuumPressureGood := VacuumPressureGetter.VacuumPressure >= GoodVacuumPressure;
	 		
{endregion}
{region "Spindle handling"}
// Spindle Speed Handling
	// Spindle is stopped if it is under 1RPM
	
	SpindleStopped := SpindleSpeedCalculator.SpindleSpeed <= 1.0;
	oPCSpindleStopped := SpindleStopped;
	// Calculate whether spindle is up to speed.  We will only say it is up to speed if it is on.
	oPCSpindleSpeedOK := SpindleOnDebounced.Q AND (SpindleSpeedCalculator.SpindleSpeed  >= (0.95 * TO_REAL(PWMDemandedSpeedCalculator.DemandedSpeed)));
// Spindle control handling
	 // The spindle frequency is in increments of 0.1hz
	 IF NOT SpindleOnDebounced.Q THEN
	 	oSpindleFrequency := 0;
	 ELSE		  
	 	oSpindleFrequency := (PWMDemandedSpeedCalculator.DemandedSpeed / HZPerRPM) * VFDHZPrecision;
	 END_IF
	 oSpindleControl.Stop := NOT SpindleOnDebounced.Q;
	 oSpindleControl.Run := SpindleOnDebounced.Q;
	
// Spindle Tool Change handling
	oPCToolPresence := iS1ToolPresence;
	oPCColletOpen := iS2ColletOpen;
	oPCSpindleOverTemp := iSpindleOverTemp;
	oValveControl.ToolRelease := (iAirPressureSensor AND SpindleStopped) AND (ToolReleaseDebounced.Q OR  iSpindleToolReleaseButton); 		  

{endregion}		  
{endregion}]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="4360" Count="34" />
      <LineId Id="4396" Count="5" />
      <LineId Id="4763" Count="0" />
      <LineId Id="4845" Count="1" />
      <LineId Id="4849" Count="0" />
      <LineId Id="4847" Count="1" />
      <LineId Id="4678" Count="0" />
      <LineId Id="4681" Count="0" />
      <LineId Id="4680" Count="0" />
      <LineId Id="4402" Count="4" />
      <LineId Id="4662" Count="0" />
      <LineId Id="4423" Count="0" />
      <LineId Id="3416" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>