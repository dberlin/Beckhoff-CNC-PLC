<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.16">
  <POU Name="PWMDemandedSpeedCalculator" Id="{d7b7f4ec-2f11-4936-85ca-0e73dfdbc7ef}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK PWMDemandedSpeedCalculator IMPLEMENTS DemandedSpeedInterface
VAR
	iInputSpeedBits AT %I* : ARRAY [1..BitArraySize] OF BYTE;
END_VAR
VAR CONSTANT
	// Array Length
	BitArraySize : UINT := 125;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="GetSpeed" Id="{213d3359-88cd-493d-995f-9cc6a93b1330}">
      <Declaration><![CDATA[METHOD GetSpeed : UINT
VAR_INST
	TempSpindleSpeed : UINT;
	LastDemandedSpeed : UINT;
	TotalOnTime : UDINT;
	TotalOffTime : UDINT;
	DutyCycle : LREAL;
	DutyCycles : ARRAY [0..20] OF LREAL;
	MovingAverage : FB_MovingAverageReal;
	Scaling : FB_ScaleBetween;
END_VAR
VAR
	I : UINT;	
	J: UINT;
	BitMask : UINT;
	MaskedBit : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// The input here is a PWM signal with a frequency of 200, a low of 1% duty, and a high of 95% duty.
// Basic Calculation for PWM pulses

 FOR I := 1 TO BitArraySize BY 1 DO
	 FOR J := 0 TO 7 BY 1 DO
		BitMask := SHL(1, J);
		MaskedBit := TO_BOOL(iInputSpeedBits[I] AND BitMask);
		IF MaskedBit THEN
			TotalOnTime := TotalOnTime + 1;
		ELSE
			TotalOffTime := TotalOffTime + 1; 
		END_IF
		// Stop at the end of the period
		IF (TotalOnTime + TotalOffTime) > Constants.UsInFrequency THEN
			EXIT;
		END_IF 
	 END_FOR
 END_FOR
 
// See if the period is up
IF (TotalOnTime + TotalOffTime) > Constants.UsInFrequency  THEN
	DutyCycle := TO_LREAL(TotalOnTime) / TO_LREAL(TotalOnTime + TotalOffTime);
	TotalOnTime := 0;
	TotalOffTime := 0;
	// Average the duty cycle and then scale the average
	MovingAverage(ValueToAdd := DutyCycle, 
				  AverageArray := DutyCycles);
	Scaling(RawValue:=MovingAverage.CurrentValue, RawValueMin:=0.0, 
					  RawValueMax:=1, ScaledValueMin:=0,
					  ScaledValueMax:=24000);
	LastDemandedSpeed := TO_UINT(Scaling.ScaledValue);
	// Make sure demanded speed is some increment of what the VFD can do. 
	LastDemandedSpeed := (LastDemandedSpeed / Constants.VFDMinRPMIncrement) * Constants.VFDMinRPMIncrement;
END_IF
GetSpeed := LastDemandedSpeed;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="PWMDemandedSpeedCalculator">
      <LineId Id="122" Count="0" />
    </LineIds>
    <LineIds Name="PWMDemandedSpeedCalculator.GetSpeed">
      <LineId Id="5" Count="33" />
      <LineId Id="46" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>